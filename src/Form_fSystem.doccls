Option Compare Database
Option Explicit

Private Const MS_VISIBLE_FOR = 3000

Private m_dict As Scripting.Dictionary
Private m_fso As Scripting.FileSystemObject
Private m_dbs As DAO.Database

Property Get Dictionary() As Scripting.Dictionary
   If m_dict Is Nothing Then Set m_dict = New Scripting.Dictionary
   Set Dictionary = m_dict
End Property

Property Get FSO() As Scripting.FileSystemObject
   If m_fso Is Nothing Then Set m_fso = New Scripting.FileSystemObject
   Set FSO = m_fso
End Property

Property Get Database() As DAO.Database
   If m_dbs Is Nothing Then Set m_dbs = CurrentDb
   Set Database = m_dbs
End Property

Sub AddToDictionary(Key As String, Item As Object)
   Me.RemoveFromDictionary Key
   Me.Dictionary.Add Key, Item
End Sub

Sub RemoveFromDictionary(Key As String)
   If Me.Dictionary.Exists(Key) Then Me.Dictionary.Remove (Key)
End Sub

Private Sub Form_Load()
   Me.TimerInterval = MS_VISIBLE_FOR
End Sub

Private Sub Form_Timer()
   Me.TimerInterval = 0
   Me.Visible = False
End Sub